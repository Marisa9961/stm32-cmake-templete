cmake_minimum_required(VERSION 3.28)

# ------------------------------ Makefile parser ----------------------------- #
include(../utils.cmake)

# Read Makefile generated by STM32CubeMX
file(READ "../../mcu/Makefile" STM32CUBEMX_MAKEFILE)
# Strip line continuations
string(REGEX REPLACE "\\\\\\\n" "" STM32CUBEMX_MAKEFILE ${STM32CUBEMX_MAKEFILE})

# -------------------------------- STM32CUBEMX ------------------------------- #
project(stm32cubemx)
add_library(stm32cubemx INTERFACE)

enable_language(C ASM)

# --------------------------------- STM32 Define ------------------------------ #
extract_makefile_flag("C_DEFS" c_defs "-D")
target_compile_definitions(stm32cubemx INTERFACE 
    ${c_defs}
)

# ---------------------------------- Lib Flags -------------------------------- #
extract_makefile_flag("LIBS" libs "-l")
target_link_libraries(stm32cubemx INTERFACE 
    ${libs}
)

# ---------------------------------- MCU Message ------------------------------ #
string(REGEX MATCH "STM32[A-Z][0-9]" DEVICE ${c_defs})

# --------------------------------- STM32 Header ------------------------------ #
target_include_directories(stm32cubemx INTERFACE
    ../../mcu/Core/Inc
    ../../mcu/Drivers/${DEVICE}xx_HAL_Driver/Inc
    ../../mcu/Drivers/${DEVICE}xx_HAL_Driver/Inc/Legacy
    ../../mcu/Drivers/CMSIS/Device/ST/${DEVICE}xx/Include
    ../../mcu/Drivers/CMSIS/Include
)

# --------------------------------- STM32 Source ------------------------------ #
extract_makefile_variable("ASM_SOURCES" asm_startup)
set(asm_startup "../../mcu/${asm_startup}")

file(GLOB c_source
    ../../mcu/Core/Src/*.c
    ../../mcu/Drivers/${DEVICE}xx_HAL_Driver/Src/*.c
)

target_sources(stm32cubemx INTERFACE
    ${asm_startup}
    ${c_source}
)
